import org.gradle.internal.os.OperatingSystem

plugins {
    id "java"
    id "application"
    id "dev.hydraulic.conveyor" version "1.13"
}

group = "com.boyninja1555"
version = "2.42"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
        vendor = JvmVendorSpec.ADOPTIUM
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

project.ext.lwjglVersion = "3.4.1"
def platforms = ["natives-windows",
                 "natives-windows-arm64",
                 "natives-macos",
                 "natives-macos-arm64",
                 "natives-linux",
                 "natives-linux-arm64"]

dependencies {
    implementation "org.jetbrains:annotations:26.0.2-1"
    implementation "com.github.JnCrMx:discord-game-sdk4j:master-SNAPSHOT"
    implementation "com.google.code.gson:gson:2.13.2"
    implementation "org.joml:joml:1.10.5"

    // LWJGL core
    implementation platform("org.lwjgl:lwjgl-bom:${project.ext.lwjglVersion}")
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-stb"

    // LWJGL natives
    platforms.each { plat ->
        runtimeOnly "org.lwjgl:lwjgl::$plat"
        runtimeOnly "org.lwjgl:lwjgl-glfw::$plat"
        runtimeOnly "org.lwjgl:lwjgl-opengl::$plat"
        runtimeOnly "org.lwjgl:lwjgl-openal::$plat"
        runtimeOnly "org.lwjgl:lwjgl-stb::$plat"
    }
}

application {
    mainClass = "${group}.floorcraft.Floorcraft"

    def jvmArgs = ["--enable-native-access=ALL-UNNAMED"]
    if (OperatingSystem.current().isMacOsX()) {
        jvmArgs << "-XstartOnFirstThread"
    }

    applicationDefaultJvmArgs = jvmArgs
}